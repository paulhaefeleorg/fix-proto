plugins {
    id 'java'
    id 'application'
    id 'me.champeau.jmh' version '0.7.2'   // JMH plugin
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    // FIX engine (classic TPS baseline)
    implementation 'org.quickfixj:quickfixj-core:2.3.1'
    implementation 'org.quickfixj:quickfixj-messages-fix44:2.3.1'

    // Chronicle libraries (OHS path)
    implementation 'net.openhft:chronicle-queue:5.27ea8'
    implementation 'net.openhft:chronicle-wire:2.27ea11'
    implementation 'net.openhft:chronicle-bytes:2.27ea7'

    // Disruptor (lock-free ring buffer)
    implementation 'com.lmax:disruptor:3.4.4'

    // Benchmarking + latency histograms
    jmh 'org.openjdk.jmh:jmh-core:1.38'
    jmhAnnotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.38'
    implementation 'org.hdrhistogram:HdrHistogram:2.1.12'

    // Testing
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'

    // Artio FIX Engine
    implementation 'uk.co.real-logic:artio-core:0.171'
    implementation 'uk.co.real-logic:artio-codecs:0.171'
    // Aeron (transport used by Artio)
    implementation 'io.aeron:aeron-driver:1.44.1'
    implementation 'io.aeron:aeron-client:1.44.1'
    // Agrona (low-latency buffers)
    implementation 'org.agrona:agrona:1.21.1'
}

application {
    // You can change this later; placeholder main class
    mainClass = 'com.fixperformance.Main'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

// Optional: regenerate FIX44 codecs explicitly

test {
    useJUnitPlatform()
}

tasks.register('runOffHeap', JavaExec) {
    group = 'application'
    description = 'Runs the OffHeap main'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.fixperformance.offheap.OffHeapMain'
}

tasks.register('runThreadPool', JavaExec) {
    group = 'application'
    description = 'Runs the ThreadPool main'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.fixperformance.threadpool.ThreadPoolMain'
}

tasks.register('runBenchmark', JavaExec) {
    group = 'application'
    description = 'Runs the Benchmark main'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'com.fixperformance.benchmark.BenchmarkMain'
}

tasks.register('refreshFix44Codecs', JavaExec) {
    group = 'codegen'
    description = 'Regenerates FIX44 codecs into src/main/java (explicit only)'
    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'uk.co.real_logic.artio.dictionary.CodecGenerationTool'
    jvmArgs '--add-exports=java.base/jdk.internal.misc=ALL-UNNAMED'
    // Write directly to source tree so code can be versioned
    args 'src/main/java', 'src/main/resources/FIX44.xml'
}

// Fat JAR that includes all runtime dependencies
tasks.register('fatJar', Jar) {
    group = 'build'
    description = 'Assembles a fat jar containing all runtime dependencies.'
    archiveClassifier = 'all'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes('Main-Class': 'com.fixperformance.Main')
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}

// Ensure fat jar is produced on build
tasks.named('build') {
    dependsOn(tasks.named('fatJar'))
}


